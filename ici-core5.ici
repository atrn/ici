/*
 * any = deepatom(any)
 */
extern deepatom(o) {
    if (isatom(o))
	return o;
    t := typeof(o);
    r := v := k := NULL;
    switch (t) {
    case "array":
	r := array();
	forall (v in o)
	    push(r, deepatom(v));
	return @r;

    case "set":
	r := set();
	forall (v in o)
	    r[deepatom(v)] = 1;
	return @r;

    case "map":
	r := map();
	forall (v, k in o)
	    r[deepatom(k)] = deepatom(v);
	super(r, deepatom(super(o)));
	return @r;

    default:
	return @o;
    }
}

// ----------------------------------------------------------------

local copymap(o, x) {
    if (v := x[o]) return v;
    r := map();
    x[o] = r;
    if (s := super(o)) {
        if (v = x[s])
            super(r, v);
        else
            super(r, copymap(s, x));
    }
    forall (v, k in o)
	r[_deepcopy(k, x)] = _deepcopy(v, x);
    return r;
}

local _deepcopy(o, x) {
    if (isatom(o)) return o;
    r := v := k := NULL;
    t := typeof(o);
    switch (t) {
    case "array":
        r = array();
        forall (v in o)
            push(r, _deepcopy(v, x));
        break;

    case "set":
        r = set();
        forall (v in o)
            r[_deepcopy(v, x)] = 1;
        break;

    case "map":
        r = copymap(o, x);
        break;

    default:
        r = copy(o);
        break;
    }
    return r;
}

/*
 * any = deepcopy(any)
 */
extern deepcopy(o) {
    return _deepcopy(o, map());
}
