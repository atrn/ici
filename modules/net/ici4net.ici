/*
 * $Id: ici4net.ici,v 1.2 2003/01/26 09:16:57 timl Exp $
 *
 * Various network related functions for ici.
 */

/*
 * string = net.class_of(ipaddr)
 *
 * Returns the network class as a string ("A", "B", "C" or "D")
 * given an IP address 'ipaddr' (also a string) in dotted decimal
 * notation.
 *
 * This --topic-- forms part of the --ici-net-- documentation.
 */
extern
class_of(ipaddr)
{
    auto        bits;

    bits = int(gettokens(ipaddr, ".")[0]);
    if ((bits & 0x80) == 0)
        return "A";
    if ((bits & 0xc0) == 0x80)
        return "B";
    if ((bits & 0xe0) == 0xc0)
        return "C";
    if ((bits & 0xf0) == 0xe0)
        return "D";
    fail("bad address");
}


static
join(v, sep)
{
    auto        e, s, seprtr, sep = "";

    s = "";
    forall (e in v)
    {
        s = sprintf("%s%s%s", s, seprtr, e);
        seprtr = sep;
    }
    return s;
}


/*
 * string = net.network_of(ipaddr)
 *
 * Returns the network address of a given an IP address 'ipaddr'
 * (also a string), both dotted decimal notation.
 *
 * This --topic-- forms part of the --ici-net-- documentation.
 */
extern
network_of(ipaddr)
{
    auto        v;

    v = gettokens(ipaddr, ".");
    switch (class_of(ipaddr))
    {
    case "A":
        v[3] = "0";
        v[2] = "0";
        v[1] = "0";
        break;
    case "B":
        v[3] = "0";
        v[2] = "0";
        break;
    case "C":
        v[3] = "0";
        break;
    }
    return join(v, ".");
}


extern
bits_for_network_class(ipaddr)
{
    auto bits = [struct
        A = 8,
        B = 16,
        C = 24,
        D = 8,
    ];
    return bits[class_of(ipaddr)];
}

