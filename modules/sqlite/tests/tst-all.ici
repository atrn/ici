// Where our test files are stored.
local test_files_dir = ".";

// Regular expression to extract the test number from the filename.
local test_file_pattern = #^tst-([0-9]+)\.ici$#;

// Extract the test number from a test file filename and return as an integer.
local test_number(filename)
{
    str = filename ~~ test_file_pattern;
    if (str == "" || str == NULL)
	fail(string(filename) + " is not a test file filename");
    if (str[0] == "0")
	str = sub(str, #^0*(\d+)#, "\\1");
    return int(str);
}

// Compare two file names through the test number they represent.
local by_test_number(a, b)
{
    return cmp(test_number(a), test_number(b));
}

// Run all the tests we can find in the test directory.
test_files := dir(test_files_dir, test_file_pattern);
sort(test_files, by_test_number);

nfailures := 0;
npasses := 0;
test := 0;

ostdin := stdin;
ostdout := stdout;
ostderr := stderr;

stdin  = fopen("/dev/null");
stdout = fopen("/dev/null", "w");
stderr = fopen("/dev/null", "w");

forall (test, index in test_files)
{
    printf(ostderr, "%d/%d - %s\n", index+1, len(test_files), test);
    try
    {
	environment := map(map(copy(super(super(scope())))));
        environment.argv := array(test);
        environment.argc := 1;
	file := fopen(test);
	parse(file, environment);
	close(file);
	++npasses;
    }
    onerror
    {
	printf(ostderr, "Failure : %s\n%s\n\n", test, error);
	++nfailures;
    }
}

printf(ostderr, "\n%d passed, %d failed\n", npasses, nfailures);
